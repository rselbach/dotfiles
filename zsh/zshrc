[[ -o interactive ]] || return

# ============================================================================
# Environment Variables
# ============================================================================

export LANG=en_US.UTF-8
export EDITOR='nvim'
export DEFAULT_USER="rselbach"
export XDG_CONFIG_HOME=~/.config
export EZA_CONFIG_DIR="${XDG_CONFIG_HOME}/eza"
export STARSHIP_CONFIG=~/.config/starship/starship.toml
export GOLANG_PROTOBUF_REGISTRATION_CONFLICT=ignore
export KUBE_PS1_NS_ENABLE=false
export GOOGLE_APPLICATION_CREDENTIALS=$HOME/.config/gcloud/application_default_credentials.json
export GOPRIVATE=github.com/hashicorp
export GPG_TTY="$(tty)"

# ============================================================================
# PATH Configuration
# ============================================================================

typeset -U path PATH

path=(
  $HOME/.local/bin
  $HOME/.opencode/bin
  $HOME/.govm/current/bin
  $HOME/devel/go/bin
  $HOME/go/bin
  $HOME/bin
  /opt/homebrew/bin
  /opt/homebrew/sbin
  /opt/homebrew/opt/fnm/bin
  /usr/local/go/bin
  /usr/local/bin
  /home/linuxbrew/.linuxbrew/bin
  /home/linuxbrew/.linuxbrew/sbin
  "/Applications/VMware Fusion.app/Contents/Library"
  $path
)

export PATH

# ============================================================================
# Shell Options
# ============================================================================

setopt AUTO_CD
setopt AUTO_PUSHD
setopt PUSHD_IGNORE_DUPS
setopt CORRECT
setopt GLOB_DOTS

# ============================================================================
# History Configuration
# ============================================================================

HISTFILE="$HOME/.zsh_history"
HISTSIZE=200000
SAVEHIST=$HISTSIZE

setopt BANG_HIST
setopt EXTENDED_HISTORY
setopt INC_APPEND_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_SAVE_NO_DUPS
setopt HIST_REDUCE_BLANKS
setopt HIST_VERIFY
setopt HIST_BEEP
setopt no_share_history

# ============================================================================
# Completions
# ============================================================================

autoload -Uz compinit && compinit -i

zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense'
if command -v carapace >/dev/null; then
  zstyle ':completion:*' format $'\e[2;37mCompleting %d\e[m'
  source <(carapace _carapace)
fi

# ============================================================================
# Tool Initialization
# ============================================================================

command -v starship >/dev/null && eval "$(starship init zsh)"
command -v zoxide >/dev/null && eval "$(zoxide init zsh)"
command -v atuin >/dev/null && eval "$(atuin init zsh)"

# SSH agent
SSH_AUTH_SOCK_FILE="$HOME/.ssh/ssh-agent.sock"
export SSH_AUTH_SOCK="$SSH_AUTH_SOCK_FILE"

ssh-add -l &>/dev/null
if [[ $? -eq 2 ]]; then
  rm -f "$SSH_AUTH_SOCK_FILE"
  eval "$(ssh-agent -s -a "$SSH_AUTH_SOCK_FILE")" >/dev/null 2>&1
fi

if ! ssh-add -l 2>/dev/null | grep -qE 'id_ed25519|id_rsa'; then
  for key in "$HOME/.ssh/id_ed25519" "$HOME/.ssh/id_rsa"; do
    [[ -f "$key" ]] && ssh-add "$key" &>/dev/null && break
  done
fi

# pyenv (lazy-loaded)
pyenv() {
  unset -f pyenv
  if command -v pyenv 1>/dev/null 2>&1; then
    eval "$(command pyenv init -)"
  fi
  pyenv "$@"
}

# fnm (lazy-loaded)
if [[ -d "/opt/homebrew/opt/fnm/bin" ]]; then
  fnm() {
    unset -f fnm
    eval "$(command fnm env)"
    fnm "$@"
  }
  node() {
    unset -f node fnm
    eval "$(command fnm env)"
    node "$@"
  }
  npm() {
    unset -f npm fnm
    eval "$(command fnm env)"
    npm "$@"
  }
fi

[[ -f "$HOME/.kube-ps1/kube-ps1.sh" ]] && source "$HOME/.kube-ps1/kube-ps1.sh"
[[ -f "$HOME/.fzf.zsh" ]] && source "$HOME/.fzf.zsh"

if [[ "$TERM_PROGRAM" == "vscode" ]] && command -v code >/dev/null; then
  . "$(code --locate-shell-integration-path zsh)"
fi

# ============================================================================
# Aliases
# ============================================================================

alias tx='tmux attach || tmux new'
alias rebase="git fetch -va && git rebase origin/main"
alias rb="git fetch -va && git rebase origin/main"
alias ls='eza'
alias ll='ls -la'
alias l='ls -la'
alias pef='ps -ef'
alias vim='nvim'

# ============================================================================
# Functions
# ============================================================================

coverhtml() {
  go test -coverprofile=/tmp/c.out "$@" || return 1
  go tool cover -html=/tmp/c.out -o /tmp/coverage.html || return 2

  if command -v open >/dev/null; then
    open /tmp/coverage.html
  elif command -v xdg-open >/dev/null; then
    xdg-open /tmp/coverage.html
  fi
}

get-github-token() {
  export GITHUB_TOKEN=$(op item --account my.1password.ca get "Github Token" --field credential --reveal)
  export HOMEBREW_GITHUB_API_TOKEN=$GITHUB_TOKEN
}

# ============================================================================
# Machine-specific (keep this last)
# ============================================================================

[[ -f "$HOME/.zsh.local" ]] && source "$HOME/.zsh.local"
